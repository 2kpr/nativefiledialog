# 
# Native File Dialog
#
# Scons build script -- GCC, Clang, Visual Studio
# Does not build test


# target arch is build arch -- extend here for cross compiling
target_arch=str(Platform())
debug = int(ARGUMENTS.get( 'debug', 0 ))

nfd_files = ['nfd_common.c']

nfd_env = Environment()

# Windows runtime library types
win_rtl = {'debug': '/MTd',     # Multithreaded Debug 
           'release': '/MT'}    # Multithreaded

def set_debug(env):
    if target_arch == 'win32':
        env.Append( CFLAGS=['/Z7',       # obj contains full symbols
                            win_rtl['debug']
                        ], 
                    LINKFLAGS=['/DEBUG'] )
    else:
        env.Append( CFLAGS=['-g'] )


def set_release(env):
    if target_arch == 'win32':
        env.Append( CFLAGS=[win_rtl['release'],  
                            '/O2'] )
    else:
        env.Append( CFLAGS=['-O3'] )
                    

def set_warnings(env):
    if target_arch == 'win32':
        env.Append( CFLAGS=['/W3'],
                    CPPDEFINES=['_CRT_SECURE_NO_WARNINGS'] )
    else:
        env.Append( CFLAGS=['-Wall', '-pedantic'] )

# Cocoa OS X builds - clang
if target_arch == 'darwin':
    nfd_files.append('nfd_cocoa.m')
    nfd_env.CC='clang -fcolor-diagnostics'

# Linux GTK+ 3 builds - GCC
elif target_arch == 'posix':
    nfd_files.append('nfd_gtk.c')
    nfd_env.ParseConfig( 'pkg-config --cflags gtk+-3.0' )

# Windows builds - Visual Studio
elif target_arch == 'win32':
    nfd_files.append('nfd_win.cpp')

if debug:
    set_debug(nfd_env)
else:
    set_release(nfd_env)

set_warnings(nfd_env)

nfd_env.Append( CPPPATH=['.','./include'] )
nfd_env.StaticLibrary( 'nfd', nfd_files )
